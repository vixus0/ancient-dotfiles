# Maintainer:  Philip Sequeira <phsequei@gmail.com>
# Contributor: Rudolf Polzer <divVerent@xonotic.org>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv-git
_gitname=mpv
pkgver=0.38497.9fadc92
pkgrel=1
pkgdesc='Video player based on MPlayer/mplayer2 (git version)'
arch=('i686' 'x86_64')
license=('GPL')
url='http://mpv.io'
depends=(
  'ffmpeg' 'hicolor-icon-theme' 'xdg-utils' 'desktop-file-utils'
  # Anything below can be left out if not needed.
  'lcms2' 'libdvdnav' 'libguess' 'libquvi' 'libxinerama' 'libxss'
  'libxv' 'lirc-utils' 'mpg123' 'smbclient' 'wayland' 'libxkbcommon'
  'libcdio-paranoia' 'portaudio'
  # Notes:
  #  libxkbcommon is only needed for wayland.
  #  If you remove libcdio-paranoia and/or portaudio, make sure to
  #  remove --enable-cdda and/or --enable-portaudio respectively from
  #  the configure below.
)
makedepends=('mesa' 'python-docutils' 'git' 'ladspa') # ladspa is optional
provides=('mpv')
conflicts=('mpv')
options=('!emptydirs')
install=mpv.install
source=('git+https://github.com/mpv-player/mpv#branch=master')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  microver="$(git log -1 --pretty=format:%h)"
  minorver="$(git rev-list --count HEAD)"
  echo "0.$minorver.$microver"
}

prepare() {
  cd "$srcdir/$_gitname"
  ./bootstrap.py

  # Fix zsh completion install location
  sed -i 's/vendor-completions/site-functions/' wscript_build.py
}

build() {
  CFLAGS="$CFLAGS -I/usr/include/samba-4.0"
  cd "$srcdir/$_gitname"

  # Warning: The configure script will automatically enable any
  # optional features it finds support for on your system. If you want
  # to build a package that will be installed on other machines, you
  # might have to explicitly disable some of them, or add them as
  # dependencies.

  ./waf configure --prefix=/usr \
    --confdir=/etc/mpv \
    --enable-joystick \
    --enable-zsh-comp \
    --enable-libmpv-shared \
    --enable-cdda \
    --enable-portaudio

  ./waf build
}

package() {
  cd "$srcdir/$_gitname"
  ./waf install --destdir="$pkgdir"

  install -d "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 etc/{input,example}.conf \
    "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv

  install -m755 TOOLS/mpv_identify.sh "$pkgdir"/usr/bin
}
